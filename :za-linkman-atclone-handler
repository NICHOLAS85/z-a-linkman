emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

if [[ "$1" = plugin ]] {
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
} else {
    local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
}

if (( ${+ICE[lman]} )) {
    [[ -d ${ZPFX}/man/man1 ]] || \
        { +zinit-message "{pre}linkman annex: {warn}Warning: {msg}Missing {url}${ZPFX}/man/man1{msg}, not installing man pages{â€¦}{rst}" && return}
    local manfiles=${dir}/**/*(.1|.1.gz)(N.)
    declare -a manfile installed attempted failed
    for manfile ( $~manfiles ); do
        (( ${attempted[(I)$manfile:t]} )) && continue
        attempted+=( $manfile:t )
        { test -f $manfile } && {
            installed+=( "%F{219}$manfile:t" )
            command ln -f -- $manfile ${ZPFX}/man/man1
        } || {
            failed+=( "%F{219}$manfile:t" )
        }
    done
    (( !OPTS[opt_-q,--quiet] )) && {
        (( ${#installed} )) && \
            +zinit-message "{pre}linkman annex: {msg}Installed man page${${${#installed}/1/}:+s}: ${(j:%f, :)installed}{rst}"
        (( ${#failed} )) && \
            +zinit-message "{pre}linkman annex: {error}Could not install man page${${${#failed}/1/}:+s}: ${(j:%f, :)failed}{rst}"
    }
}
